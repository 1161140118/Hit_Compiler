# 格式： A -> B C D @a
# Pros （多个）过程 P
# Decs （多个）声明 D
# Sens （多个）可执行语句 S
# @a 优先级更高字符 a

####################

# 综合

P' -> Decs Pros
Pros -> P Pros
Pros -> P
Pros -> $
P -> { Decs Sens }
P -> { Decs }
P -> { Sens }
Decs -> D Decs
Decs -> D
Decs -> $
Sens -> S Sens
Sens -> S


#####################

# 声明语句

D -> T ID ;
D -> T ID = E ;
T -> Type Array

## 声明函数
P -> func ID ( Dlist ) { Decs }
P -> func ID ( Dlist ) { Sens }
P -> func ID ( Dlist ) { Decs Sens }
Dlist -> D , Dlist
Dlist -> D
Dlist -> $   

## 声明结构体
D -> struct ID { D }

Type -> int
Type -> char
Type -> float
Type -> void

## 数组下标
Array -> [ INT ] Array
Array -> $

#####################

# 赋值语句

S -> ID = E ;
S -> L = E ;

E -> E + E  @*
E -> E * E
E -> ( E )
E -> ID
E -> Const
E -> L

L -> ID [ E ]
L -> L [ E ]

Const -> INT
Const -> FLOAT
Const -> BOOL
Const -> STR

#####################

# 控制语句
# B 布尔表达式

S -> if B P             @else     
S -> if B P else P
S -> while B P
B -> B || B     @! @&&
B -> B && B     @!
B -> ! B
B -> ( B )
B -> E < E
B -> E <= E
B -> E > E
B -> E >= E
B -> E == E
B -> E != E
B -> true
B -> false

#####################

# 过程调用

S -> call ID ( Es ) ;
Es -> E , Es
Es -> E
Es -> $